import Mathlib.Data.Erased
import Mathlib.MeasureTheory.Measure.Decomposition.Lebesgue
import Mathlib.MeasureTheory.Integral.Bochner.Basic
import Mathlib.MeasureTheory.Measure.GiryMonad

import SciLean.Algebra.IsAffineMap
import SciLean.Analysis.Scalar
import SciLean.Logic.Function.Bijective
import SciLean.MeasureTheory.WeakIntegral
import SciLean.Meta.Notation.Do
import SciLean.Meta.SimpAttr
import SciLean.Probability.SimpAttr
import SciLean.Util.Limit

-- import Mathlib.Control.Random
-- import SciLean.Mathlib.MeasureTheory.WeakIntegral
-- import SciLean.Core.FunctionPropositions.IsAffineMap
-- import SciLean.Core.Objects.Scalar
-- import SciLean.Core.Notation



open MeasureTheory ENNReal BigOperators Finset

namespace SciLean

abbrev erase (a : Î±) : Erased Î± := .mk a

@[simp,simp_core]
theorem erase_out {Î±} (a : Î±) : (erase a).out = a := by simp[erase]


/-- `x : Rand X` is a random variable of type `X`

You can:
  - generate sample with `x.get : IO X`
  - get probability measure with `x.â„™ : Measure X`

The internal fields `spec` and `rand` are just an internal implementation of `Rand` and should not
be accessed by normal users.

TODO: Hide implementation using quotients or something like that
-/
structure Rand (X : Type _)  where
  /-- `spec` defines a probability measure by computing an expectation. This means if `x : Rand X`
  corresponds to a probability measure `Î¼` then for `Ï† : X â†’ â„`
  ```
  x.spec.out Ï† = âˆ« x, Ï† x âˆ‚Î¼
  ```

  Using `(Xâ†’â„)â†’â„` instead of `Measure X` for the specification of random variables has the
  advantage that we can reuse Lean's `do` notation.
  -/
  spec : Erased ((Xâ†’â„)â†’â„)
  /-- `rand` is a pseudo randon number generator implemented using the "Standard" number generator
  -/
  rand : StateM StdGen X


namespace Rand

def _root_.Function.IsMeasure {X} [MeasurableSpace X] (F : (X â†’ â„) â†’ â„) : Prop :=
  âˆƒ Î¼ : Measure X, âˆ€ (f : X â†’ â„), F f = âˆ« x, f x âˆ‚Î¼

open Classical in

/-- Probability measure of a random variable -/
noncomputable
def â„™ {X} [MeasurableSpace X] (r : Rand X) : Measure X :=
  if h : r.spec.out.IsMeasure then
    choose h
  else
    0

/-- Specification of `x : Rand X` is really saying that it is a probability measure. -/
class LawfulRand (x : Rand X) [MeasurableSpace X] : Prop where
  is_measure : x.spec.out.IsMeasure
  is_prob : IsProbabilityMeasure x.â„™

set_option deprecated.oldSectionVars true

variable {X Y Z : Type _}
  [MeasurableSpace X] [MeasurableSingletonClass X]
  [MeasurableSpace Y] [MeasurableSingletonClass Y]

instance instIsProbabilityMeasureâ„™ (x : Rand X) [inst : LawfulRand x] : IsProbabilityMeasure (x.â„™) := inst.is_prob


/-- Extensionality of random variable.

WARNING: This theorem is inconsistent!!! The random generators `x.rand` and `y.rand` might differ.
         We are not trying to model pseudo-random numbers. We assume that every random number
         generator is a true random number generator. Thus the result of any probabilistic program
         should be independent on the exact generator up to some randomness.

TODO: We might quotient all the random number generators corresponding to the measure `x.â„™`  under
      the assumption that they are all true random generators. I believe that such type would be
      a singleton i.e. all the random number generators are all the same.
-/
@[ext]
axiom ext (x y : Rand X) : x.spec.out = y.spec.out â†’ x = y


/-- Generate rundom number using IO randomness -/
def get (x : Rand X) : IO X := do
  let stdGen â† IO.stdGenRef.get
  let (res, new) := x.rand stdGen
  let _ â† IO.stdGenRef.set new
  pure res


----------------------------------------------------------------------------------------------------
-- Monadic structure -------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

instance : Monad Rand where
  pure x := {
    spec := erase (fun Ï† => Ï† x),
    rand := pure x
  }
  bind x f := {
    spec := default, -- erase (fun Ï† => x.spec.out (fun x => (f x).spec.out Ï†)),
    rand := bind x.rand (fun x => (f x).rand)
  }


instance : LawfulMonad Rand where
  bind_pure_comp := by intros; rfl
  bind_map       := by intros; rfl
  pure_bind      := sorry_proof -- by intros; ext; simp[Bind.bind,Pure.pure]
  bind_assoc     := by intros; ext; simp[Bind.bind,Pure.pure]
  map_const      := by intros; ext; rfl
  id_map         := sorry_proof -- by intros; ext; simp[Bind.bind,Pure.pure,id,Functor.map]
  seqLeft_eq     := by intros; ext; simp[Bind.bind,Pure.pure,Seq.seq,Function.const,Functor.map,SeqLeft.seqLeft]
  seqRight_eq    := by intros; ext; simp[Bind.bind,Pure.pure,Seq.seq,Function.const,Functor.map,SeqRight.seqRight]
  pure_seq       := by intros; ext; simp[Bind.bind,Pure.pure,Seq.seq,Functor.map]


-- this needs some integrability and lawfulness of Rand
theorem swap_bind (f : X â†’ Y â†’ Z) (x : Rand X) (y : Rand Y) :
    (do let x' â† x; let y' â† y; pure (f x' y'))
    =
    (do let y' â† y; let x' â† x; pure (f x' y')) := by
  sorry_proof


@[simp, simp_core]
theorem pure_â„™ (x : X) : (pure x : Rand X).â„™ = Measure.dirac x := sorry_proof


----------------------------------------------------------------------------------------------------
-- Arithmetics -------------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

instance [Add X] : HAdd X (Rand X) (Rand X) := âŸ¨fun x' x => do
  let x'' â† x
  pure (x' + x'')âŸ©

instance [Add X] : HAdd (Rand X) X (Rand X) := âŸ¨fun x x' => do
  let x'' â† x
  pure (x'' + x')âŸ©

-- instance [Add X] : HAdd (Rand X) (Rand X) (Rand X) := âŸ¨fun x y =>
--   let x' ~ x
--   let y' ~ y
--   pure (x' + y')âŸ©

-- todo: add simp theorems that inline these operations

----------------------------------------------------------------------------------------------------
-- Simple Random Variable functions ----------------------------------------------------------------
----------------------------------------------------------------------------------------------------

abbrev map (r : Rand X) (f : X â†’ Y) : Rand Y := do
  let x' â† r
  return f x'

/-- Marginal distribution for the first component of a pair. -/
abbrev fst (r : Rand (XÃ—Y)) : Rand X := do
  let (x,_) â† r
  return x

/-- Marginal distribution for the second component of a pair. -/
abbrev snd (r : Rand (XÃ—Y)) : Rand Y := do
  let (_,y) â† r
  return y


@[simp, simp_core]
theorem map_â„™  (r : Rand X) (f : X â†’ Y) :
  (r.map f).â„™ = r.â„™.map f := sorry_proof

@[simp, simp_core]
theorem map_â„™'  (r : Rand X) (f : X â†’ Y) :
  (f <$> r).â„™ = r.â„™.map f := sorry_proof


----------------------------------------------------------------------------------------------------
-- Expected Value ----------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

section ExpectedValue

variable
  {R} [RealScalar R]
  [AddCommGroup Y] [Module â„ Y] [TopologicalSpace Y] [LocallyConvexSpace â„ Y]
  [AddCommGroup Z] [Module â„ Z] [TopologicalSpace Z] [LocallyConvexSpace â„ Z]
  {U} [AddCommGroup U] [Module â„ U] [TopologicalSpace U] [LocallyConvexSpace â„ U]
  -- {U} [AddCommGroup U] [TopologicalSpace U] [TopologicalAddGroup U] [Module â„ U] [LocallyConvexSpace â„ U]

noncomputable
def E (r : Rand X) (Ï† : X â†’ Y) : Y := weakIntegral r.â„™  Ï†

@[simp, simp_core, rand_push_E]
theorem pure_ğ”¼ (x : X) (Ï† : X â†’ Y) :
    (pure (f:=Rand) x).E Ï† = Ï† x := by simp [E]

-- What are the right assumptions here? Lambda lawfulness of `x` and `f x'` and integrability of `Ï†`
@[rand_push_E]
theorem bind_E (r : Rand X) (f : X â†’ Rand Y) (Ï† : Y â†’ Z) :
    (r >>= f).E Ï† = r.E (fun x' => (f x').E Ï†) := by simp[E]; sorry_proof

-- todo: We might want this to hold without lawfulness
-- consider adding as a property inside of `Distribution` or `Rand`
@[simp, simp_core, rand_push_E]
theorem E_zero (r : Rand X) :
    r.E (fun _ => (0 : Y)) = 0 := by simp[E]

@[simp, simp_core, add_pull, rand_push_E]
theorem E_add (r : Rand X) (Ï† Ïˆ : X â†’ U)
    (hÏ† : WeakIntegrable Ï† r.â„™) (hÏˆ : WeakIntegrable Ïˆ r.â„™) :
    r.E (fun x => Ï† x + Ïˆ x) = r.E Ï† + r.E Ïˆ := by
  simp[E]; rw[weakIntegral_add] <;> assumption

@[simp, simp_core, smul_pull, rand_push_E]
theorem E_smul (r : Rand X) (Ï† : X â†’ â„) (y : Y) :
    r.E (fun x' => Ï† x' â€¢ y) = r.E Ï† â€¢ y := by sorry_proof

@[simp, simp_core, rand_push_E]
theorem map_E (f : X â†’ Y) {r : Rand X} {Ï† : Y â†’ Z} :
    (f <$> r).E Ï†
    =
    r.E (Ï† âˆ˜ f) := by
  simp[E]
  rw[weakIntegral_map sorry_proof sorry_proof]
  rfl

theorem reparameterize [Nonempty X] (f : X â†’ Y) (hf : f.Injective) {r : Rand X} {Ï† : X â†’ Z} :
    r.E Ï†
    =
    let invf := f.invFun
    (r.map f).E (fun y => Ï† (invf y)) := by
  simp [E]
  rw[weakIntegral_map sorry_proof sorry_proof]
  simp [E,Function.invFun_comp' hf]

section Mean

variable [AddCommGroup X] [Module â„ X] [TopologicalSpace X] [LocallyConvexSpace â„ X]

noncomputable
def mean (r : Rand X) : X := r.E id

@[rand_pull_E]
theorem expectedValue_as_mean (x : Rand X) (Ï† : X â†’ Y) :
    x.E Ï† = (x.map Ï†).mean := by
  simp [bind,mean,pure,E]
  rw[weakIntegral_map sorry_proof sorry_proof]
  rfl

@[simp,simp_core]
theorem pure_mean (x : X) : (pure (f:=Rand) x).mean = x := by simp[mean]

@[rand_push_E]
theorem bind_mean (x : Rand X) (f : X â†’ Rand Y) :
    (x >>= f).mean = x.E (fun x' => (f x').mean) := by simp[mean,rand_push_E]

@[simp, simp_core, rand_push_E]
theorem map_mean (f : X â†’ Y) {r : Rand X} :
    (f <$> r).mean
    =
    r.E f := by simp[mean]

theorem mean_add  (x : Rand X) (x' : X) : x.mean + x' = (x  + x').mean := by
  simp[HAdd.hAdd,mean,E,pure,bind]; sorry_proof
theorem mean_add' (x : Rand X) (x' : X) : x' + x.mean = (x' +  x).mean := by
  simp[HAdd.hAdd,mean,E,pure,bind]; sorry_proof

set_option linter.unusedVariables false in
theorem mean_affine (x : Rand X) (f : X â†’ Y) (hf : IsAffineMap â„ f) :
   f x.mean = (do let x' â† x; return (f x')).mean := sorry_proof

end Mean

variable (R)
variable [Module R Y] [IsScalarTower â„ R Y]
/-- Estimate expected value of `f x`. -/
def estimateE (n : â„•) (x : Rand X) (f : X â†’ Y) : Rand Y := do
  let mut y := (0:Y)
  for _ in [0:n] do
    let x' â† x
    y += f x'
  return ((1:R)/(n:R)) â€¢ y


-- is this right? Do I need `mean` there?
-- theorem estimateE_affine
--     [AddCommGroup X] [Module â„ X] [Module R X] [IsScalarTower â„ R X] [TopologicalSpace X]
--     (n : â„•) (x : Rand X) (f : X â†’ Y) (hf : IsAffineMap â„ f) :
--     (estimateE R n x f).mean = f (estimateE R n x id).mean := sorry_proof

theorem E_eq_mean_estimateE (n : â„•) (x : Rand X) (f : X â†’ Y) :
    x.E f = (estimateE R n x f).mean := sorry_proof

-- what conditions do we need on `g`? Probably continuity?
theorem E_eq_limit_estimateE (x : Rand X) (f : X â†’ Y) (g : Y â†’ Z) :
    g (x.E f)
    =
    limit n â†’ âˆ,
      let y := (estimateE R n x f).mean
      g y := sorry_proof


variable {R}

end ExpectedValue


----------------------------------------------------------------------------------------------------
-- Probability density function --------------------------------------------------------------------
----------------------------------------------------------------------------------------------------


variable
  {R} [RealScalar R]
  [MeasurableSpace X]
  [MeasurableSpace Y]


variable (R)
/-- Probability density function of `x` w.r.t. the measure `Î½`. -/
noncomputable
def pdf (x : Rand X) (Î½ : Measure X := by volume_tac) : X â†’ R :=
  fun x' => Scalar.ofReal R (Measure.rnDeriv x.â„™ Î½ x').toReal
variable {R}

@[simp,simp_core]
theorem pdf_wrt_self (x : Rand X) [LawfulRand x] : x.pdf R x.â„™ = 1 := sorry_proof

@[simp,simp_core]
theorem bind_pdf (Î½ : Measure Y) (x : Rand X) (f : X â†’ Rand Y) :
    (x >>= f).pdf R Î½ = fun y => âˆ« x', ((f x').pdf R Î½ y) âˆ‚x.â„™ := by
  funext y; simp[Rand.pdf,Bind.bind,Pure.pure]; sorry_proof

@[simp,simp_core]
theorem ite_pdf (c) [Decidable c] (t e : Rand X) (Î¼ : Measure X) :
    (if c then t else e).pdf R Î¼ = (if c then t.pdf R Î¼ else e.pdf R Î¼) := by
  if h : c then
    simp [h]
  else
    simp [h]


----------------------------------------------------------------------------------------------------
-- Combine -----------------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

variable [MeasureSpace R]
variable (R)
@[inline] -- inlining seems to have quite implact on performance
def _root_.SciLean.uniformI [MeasureSpace R] : Rand R := {
  spec := default
    -- erase (fun Ï† => weakIntegral (volume.restrict (Set.Icc (0:R) (1:R))) Ï† )
  rand :=
    fun g => do
    let N := stdRange.2
    let (n,g) := stdNext g
    let y := (NatCast.natCast n : R) / (NatCast.natCast N : R)
    pure (y, g)
}
variable {R}

/-- Draw `x` with probability `1-Î¸` and `y` with probability `Î¸`. -/
def combine (x y : Rand X) (Î¸ : R) : Rand X := do
  let Î¸' â† uniformI R
  if Î¸ â‰¤ Î¸' then
    x
  else
    y

/-- `x +[Î¸] y` return random variable `(1-Î¸)*x + Î¸*y`.
In other words
- `x` is generated with probability `1-Î¸`
- `y` is generated with probability `Î¸` -/
scoped macro x:term:65 " +[" Î¸:term "] " y:term:64 : term => `(term| combine $x $y $Î¸)


open Lean Parser
@[app_unexpander Rand.combine] def unexpandRandCombine : Lean.PrettyPrinter.Unexpander
| `($(_) $x $y $Î¸) => do Pure.pure (â† `(term| $x +[$Î¸] $y)).raw
| _ => throw ()


@[rand_simp]
theorem combine_pdf (x y : Rand X) (Î¼ : Measure X) (Î¸ : R) :
    (x +[Î¸] y).pdf R Î¼
    =
    fun x' => (1-Î¸) * x.pdf R Î¼ x' + Î¸ * y.pdf R Î¼ x' := sorry_proof


----------------------------------------------------------------------------------------------------
