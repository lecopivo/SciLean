import SciLean.Analysis.AdjointSpace.Basic

import SciLean.Analysis.Normed.IsContinuousLinearMap
import SciLean.Analysis.Normed.Norm2

import SciLean.Tactic.FunTrans.Elab
import SciLean.Tactic.FunTrans.Attr

set_option linter.unusedVariables false

open RCLike

open scoped ComplexConjugate

set_option deprecated.oldSectionVars true

variable {ğ•œ E F G : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
variable [AdjointSpace ğ•œ E] [AdjointSpace ğ•œ F] [AdjointSpace ğ•œ G]

set_default_scalar ğ•œ

/-! ### Adjoint operator -/

open AdjointSpace SciLean

variable [CompleteSpace E] [CompleteSpace G] [CompleteSpace F]


variable (ğ•œ)
open Classical in
/-- The adjoint of a bounded operator from Hilbert space `E` to Hilbert space `F`. -/
@[fun_trans]
noncomputable
def adjoint (f : E â†’ F) (y : F) : E :=
  if h : âˆƒ g : F â†’ E, âˆ€ x y, âŸªf x, yâŸ« = âŸªx, g yâŸ« then
    choose h y
  else
    0
variable {ğ•œ}


postfix:1000 "â€ " => adjoint defaultScalar%


theorem adjoint_ex [CompleteSpace E] [CompleteSpace F] (A : E â†’ F) (hA : IsContinuousLinearMap ğ•œ A) :
    âˆ€ x y, âŸªA x, yâŸ« = âŸªx, (Aâ€ ) yâŸ« := sorry_proof

theorem adjoint_clm {A : E â†’ F} (hA : IsContinuousLinearMap ğ•œ A) : IsContinuousLinearMap ğ•œ (Aâ€ ) :=
    sorry_proof

/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_left (A : E â†’ F) (hA : IsContinuousLinearMap ğ•œ A) (x : E) (y : F) :
    âŸª(Aâ€ ) y, xâŸ« = âŸªy, A xâŸ« := by
  rw[â† AdjointSpace.conj_symm]
  rw[â† adjoint_ex _ hA]
  rw[AdjointSpace.conj_symm]


/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_right (A : E â†’ F) (hA : IsContinuousLinearMap ğ•œ A) (x : E) (y : F) :
    âŸªx, (Aâ€ ) yâŸ« = âŸªA x, yâŸ« := by
  rw[â† adjoint_ex _ hA]

/-- The adjoint is involutive. -/
@[simp]
theorem adjoint_adjoint (A : E â†’ F) (hA : IsContinuousLinearMap ğ•œ A) : Aâ€ â€  = A := by
  funext u
  apply AdjointSpace.ext_inner_left ğ•œ
  intro v
  rw[â† adjoint_ex _ (adjoint_clm hA)]
  apply adjoint_inner_left _ hA


theorem adjoint.arg_y.IsLinearMap (A : E â†’ F) : IsLinearMap ğ•œ (fun y => adjoint ğ•œ A y) := by
  constructor
  Â· sorry_proof
  Â· intros r x
    apply AdjointSpace.ext_inner_right ğ•œ; intro v
    rw[AdjointSpace.inner_smul_left]
    if hA : IsContinuousLinearMap ğ•œ A then
      simp[adjoint_inner_left (hA:=hA)]
      rw[AdjointSpace.inner_smul_left]
    else
      sorry_proof


set_option linter.hashCommand false in
#generate_linear_map_simps adjoint.arg_y.IsLinearMap

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
theorem adjoint_comp (A : F â†’ G) (B : E â†’ F)
    (hA : IsContinuousLinearMap ğ•œ A) (hB : IsContinuousLinearMap ğ•œ B) :
    (A âˆ˜ B)â€  = Bâ€  âˆ˜ Aâ€  := by
  funext u
  apply AdjointSpace.ext_inner_left ğ•œ
  intro v; dsimp
  rw[â† adjoint_ex _ (by fun_prop), â† adjoint_ex _ hB,â† adjoint_ex _ hA]
  rfl

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
theorem eq_adjoint_iff (A : E â†’ F) (B : F â†’ E) (hB : IsContinuousLinearMap ğ•œ B) :
    A = Bâ€  â†” âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ« := by
  constructor
  Â· intro h x y; rw[h,adjoint_inner_left _ hB]
  Â· intro h; funext u
    apply AdjointSpace.ext_inner_right ğ•œ
    intro v
    rw[adjoint_inner_left _ hB]
    apply h u v



----------------------------------------------------------------------------------------------------

namespace adjoint


variable
  {K : Type _} [RCLike K]
  {X : Type _} [NormedAddCommGroup X] [AdjointSpace K X] [CompleteSpace X]
  {Y : Type _} [NormedAddCommGroup Y] [AdjointSpace K Y] [CompleteSpace Y]
  {Z : Type _} [NormedAddCommGroup Z] [AdjointSpace K Z] [CompleteSpace Z]
  {Î¹ : Type _} {n} [IndexType Î¹ n] [Fold Î¹]
  {E : Î¹ â†’ Type _} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, AdjointSpace K (E i)] [âˆ€ i, CompleteSpace (E i)]

set_default_scalar K

@[fun_trans]
theorem adjoint_id :
    (fun x : X => x)â€  = fun x => x := by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  intros; rfl


@[fun_trans]
theorem const_rule :
    (fun (x : X) => (0 : Y))â€  = fun x => 0 := by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp

@[fun_trans]
theorem proj_rule [DecidableEq Î¹]
    (i : Î¹) :
    (fun (f : (i' : Î¹) â†’ E i') => f i)â€ 
    =
    fun x => (fun j => if h : i=j then h â–¸ x else 0) := by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  intro x y
  rw[inner_forall_split]
  sorry_proof

@[fun_trans]
theorem prod_rule
    (f : X â†’ Y) (g : X â†’ Z)
    (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => (f x, g x))â€ 
    =
    fun yz =>
      let xâ‚ := (fâ€ ) yz.1
      let xâ‚‚ := (gâ€ ) yz.2
      xâ‚ + xâ‚‚ :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  intro (y,z) x
  rw[AdjointSpace.inner_add_left]
  simp (disch:=fun_prop) [adjoint_inner_left]
  rfl

@[fun_trans]
theorem comp_rule
    (f : Y â†’ Z) (g : X â†’ Y)
    (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f (g x))â€ 
    =
    fun z =>
      let y := (fâ€ ) z
      let x := (gâ€ ) y
      x := by
  funext u
  apply AdjointSpace.ext_inner_left K
  intro v; dsimp
  rw[â† adjoint_ex _ (by fun_prop), adjoint_ex _ hf,â† adjoint_ex _ hg]


@[fun_trans]
theorem let_rule
    (f : X â†’ Y â†’ Z) (g : X â†’ Y)
    (hf : IsContinuousLinearMap K (fun xy : XÃ—Y => f xy.1 xy.2)) (hg : IsContinuousLinearMap K g) :
    (fun x => let y := g x; f x y)â€ 
    =
    fun z =>
      let xy := ((fun (x,y) => f x y)â€ ) z
      let x' := (gâ€ ) xy.2
      xy.1 + x' :=
by
  have h : (fun x => let y := g x; f x y)â€ 
           =
           (fun x => (x, g x))â€  âˆ˜ (fun (x,y) => f x y)â€ 
         := comp_rule (K:=K) (f:=_) (g:=(fun x => (x, g x))) (hf:=hf) (hg:=by fun_prop)
  rw[h]
  fun_trans
  rfl


@[fun_trans]
theorem pi_rule
    (f : X â†’ (i : Î¹) â†’ E i) (hf : âˆ€ i, IsContinuousLinearMap K (f Â· i)) :
    (fun (x : X) (i : Î¹) => f x i)â€ 
    =
    (fun x' => âˆ‘á´µ i, ((f Â· i)â€ ) (x' i)) := by

  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  intro x y
  sorry_proof
  -- rw[AdjointSpace.sum_inner]
  -- simp (disch:=fun_prop) [adjoint_inner_left]
  -- rfl


end adjoint


--------------------------------------------------------------------------------
-- Function Rules --------------------------------------------------------------
--------------------------------------------------------------------------------

variable
  {K : Type _} [RCLike K]
  {X : Type _} [NormedAddCommGroup X] [AdjointSpace K X] [CompleteSpace X]
  {Y : Type _} [NormedAddCommGroup Y] [AdjointSpace K Y] [CompleteSpace Y]
  {Z : Type _} [NormedAddCommGroup Z] [AdjointSpace K Z] [CompleteSpace Z]
  {Î¹ : Type _} [Fintype Î¹]
  {E : Î¹ â†’ Type _} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, AdjointSpace K (E i)] [âˆ€ i, CompleteSpace (E i)]

open SciLean

set_default_scalar K


theorem SciLean.adjoint_wrt_prod
    {f : X â†’ Y â†’ Z} (hf : IsContinuousLinearMap K â†¿f := by fun_prop) :
    adjoint K (fun xy : XÃ—Y => f xy.1 xy.2)
    =
    fun (z : Z) =>
      let x := adjoint K (f Â· 0) z
      let y := adjoint K (f 0 Â·) z
      (x,y) := sorry_proof


-- Prod ------------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Prod.fst.arg_self.adjoint_rule
  (f : X â†’ YÃ—Z) (hf : SciLean.IsContinuousLinearMap K f)
  : (fun x => (f x).1)â€ 
    =
    fun y => (fâ€ ) (y,0) :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,inner_prod_split]

@[fun_trans]
theorem Prod.snd.arg_self.adjoint_rule
  (f : X â†’ YÃ—Z) (hf : SciLean.IsContinuousLinearMap K f)
  : (fun x => (f x).2)â€ 
    =
    fun z => (fâ€ ) (0,z) :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,inner_prod_split]


-- HAdd.hAdd -------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem HAdd.hAdd.arg_a0a1.adjoint_rule
    (f g : X â†’ Y) (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f x + g x)â€ 
    =
    fun y =>
      let xâ‚ := (fâ€ ) y
      let xâ‚‚ := (gâ€ ) y
      xâ‚ + xâ‚‚ := by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_add_left,AdjointSpace.inner_add_right]



-- HSub.hSub -------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem HSub.hSub.arg_a0a1.adjoint_rule
    (f g : X â†’ Y) (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f x - g x)â€ 
    =
    fun y =>
      let xâ‚ := (fâ€ ) y
      let xâ‚‚ := (gâ€ ) y
      xâ‚ - xâ‚‚ := by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_sub_left,AdjointSpace.inner_sub_right]


-- Neg.neg ---------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Neg.neg.arg_a0.adjoint_rule
  (f : X â†’ Y) (hf : IsContinuousLinearMap K f)
  : (fun x => - f x)â€ 
    =
    fun y => - (fâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_neg_left,AdjointSpace.inner_neg_right]


-- HMul.hmul -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HMul.hMul.arg_a0.adjoint_rule
  (c : K) (f : X â†’ K) (hf : IsContinuousLinearMap K f)
  : (fun x => f x * c)â€ 
    =
    fun y => conj c â€¢ (fâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl

open ComplexConjugate in
@[fun_trans]
theorem HMul.hMul.arg_a1.adjoint_rule
  (c : K) (f : X â†’ K) (hf : IsContinuousLinearMap K f)
  : (fun x => c * f x)â€ 
    =
    fun y => conj c â€¢ (fâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl


-- SMul.smul -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HSMul.hSMul.arg_a0.adjoint_rule
  (y : Y) (f : X â†’ K) (hf : IsContinuousLinearMap K f)
  : (fun x => f x â€¢ y)â€ 
    =
    fun y' => âŸªy, y'âŸ« â€¢ (fâ€ ) 1 :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl

open ComplexConjugate in
@[fun_trans]
theorem HSMul.hSMul.arg_a1.adjoint_rule
  (c : K) (g : X â†’ Y) (hg : IsContinuousLinearMap K g)
  : (fun x => c â€¢ g x)â€ 
    =
    fun y => (conj c) â€¢ (gâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]


-- HDiv.hDiv -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HDiv.hDiv.arg_a0.adjoint_rule
  (f : X â†’ K) (c : K)
  (hf : IsContinuousLinearMap K f)
  : (fun x => f x / c)â€ 
    =
    fun y => (conj c)â»Â¹ â€¢ (fâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  simp [div_eq_mul_inv]
  intros; ac_rfl



-- Finset.sum ------------------------------------------------------------------
--------------------------------------------------------------------------------

open BigOperators in
@[fun_trans]
theorem Finset.sum.arg_f.adjoint_rule
  (f : X â†’ Î¹ â†’ Y) (hf : âˆ€ i, IsContinuousLinearMap K (f Â· i)) (A : Finset Î¹)
  : (fun x => Finset.sum A fun i => f x i)â€ 
    =
    (fun y => Finset.sum A fun i => ((f Â· i)â€ ) y) :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.sum_inner,AdjointSpace.inner_sum]


-- @[fun_trans]
-- theorem sum.arg_f.adjoint_rule {Î¹} [IndexType Î¹]
--   (f : X â†’ Î¹ â†’ Y) (hf : âˆ€ i, IsContinuousLinearMap K (f Â· i))
--   : (fun x => sum fun i => f x i)â€ 
--     =
--     (fun y => sum fun i => ((f Â· i)â€ ) y) :=
-- by
--   rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
--   sorry_proof

@[fun_trans]
theorem SciLean.IndexType.sum.arg_f.adjoint_rule {Î¹ n} [IndexType Î¹ n] [Fold Î¹]
  (f : X â†’ Î¹ â†’ Y) (hf : âˆ€ i, IsContinuousLinearMap K (f Â· i))
  : (fun x => âˆ‘á´µ i, f x i)â€ 
    =
    (fun y => âˆ‘á´µ i, ((f Â· i)â€ ) y) :=
by
  rw[â† (eq_adjoint_iff _ _ (by fun_prop)).2]
  sorry_proof
  -- simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.sum_inner,AdjointSpace.inner_sum]


-- d/ite -----------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem ite.arg_te.adjoint_rule
  (c : Prop) [dec : Decidable c]
  (t e : X â†’ Y) (ht : IsContinuousLinearMap K t) (he : IsContinuousLinearMap K e)
  : (fun x => if c then t x else e x)â€ 
    =
    fun y =>
      if c then (tâ€ ) y else (eâ€ ) y :=
by
  induction dec
  case isTrue h  => ext y; simp[h]
  case isFalse h => ext y; simp[h]

@[fun_trans]
theorem dite.arg_te.adjoint_rule
  (c : Prop) [dec : Decidable c]
  (t : c  â†’ X â†’ Y) (ht : âˆ€ p, IsContinuousLinearMap K (t p))
  (e : Â¬c â†’ X â†’ Y) (he : âˆ€ p, IsContinuousLinearMap K (e p))
  : (fun x => if h : c then t h x else e h x)â€ 
    =
    fun y =>
      if h : c then ((t h Â·)â€ ) y else ((e h Â·)â€ ) y :=
by
  induction dec
  case isTrue h  => ext y; simp[h]
  case isFalse h => ext y; simp[h]



-- Inner -----------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Inner.inner.arg_a1.adjoint_rule
  (f : X â†’ Y) (hf : IsContinuousLinearMap K f) (y : Y)
  : (fun x => âŸªy, f xâŸ«)â€ 
    =
    fun z => z â€¢ (fâ€ ) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by sorry_proof)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.conj_symm,mul_comm]


section OnRealSpace

variable
  {R K : Type*} [RealScalar R] [Scalar R K] [ScalarSMul R K] [ScalarInner R K]
  {X : Type*} [NormedAddCommGroup X] [AdjointSpace R X] [AdjointSpace K X] [CompleteSpace X]
  {Y : Type*} [NormedAddCommGroup Y] [AdjointSpace R Y] [AdjointSpace K Y] [CompleteSpace Y]
  -- maybe [IsScalarTower R K X] [IsScalarTower R K Y] ?
  -- This should be done properly with proofs to make sure it is correct
open SciLean

@[fun_trans]
theorem Inner.inner.arg_a1.adjoint_rule_real
  (f : X â†’ Y) (hf : IsContinuousLinearMap R f) (y : Y)
  : adjoint R (fun x => âŸªy, f xâŸ«[K])
    =
    fun z => z â€¢ (adjoint R f) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by sorry_proof)).2]
  sorry_proof

open ComplexConjugate in
@[fun_trans]
theorem Inner.inner.arg_a0.adjoint_rule
  (f : X â†’ Y) (hf : IsContinuousLinearMap R f) (y : Y)
  : adjoint R (fun x => âŸªf x, yâŸ«[K])
    =
    fun z => (conj z) â€¢ (adjoint R f) y :=
by
  rw[â† (eq_adjoint_iff _ _ (by sorry_proof)).2]
  sorry_proof


end OnRealSpace



--------------------------------------------------------------------------------

section IsContinuousLinearMap

variable
  {R K : Type*} [RealScalar R] [Scalar R K] [ScalarSMul R K]
  {X : Type*} [TopologicalSpace X] [AddCommMonoid X] [Module R X] [Module K X]
  {Y : Type*} [NormedAddCommGroup Y] [AdjointSpace R Y] [AdjointSpace K Y] [CompleteSpace Y]

-- set_default_scalar R

-- Inner -----------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_prop]
theorem Inner.inner.arg_a0.IsContinuousLinearMap_rule
  (f : X â†’ Y) (hf : IsContinuousLinearMap R f) (y : Y)
  : IsContinuousLinearMap R fun x => âŸªf x, yâŸ«[K] :=
by
  constructor
  Â· constructor
    Â· intros
      rw[hf.linear.map_add]
      rw[AdjointSpace.inner_add_left]
    Â· intro c x
      rw[hf.linear.map_smul]
      calc _ = âŸª(c â€¢ (1:K)) â€¢ f x, yâŸ«[K] := by simp
           _ = (conj (c â€¢ (1:K))) * âŸªf x, yâŸ«[K] := by rw[AdjointSpace.inner_smul_left]
           _ = c â€¢ âŸªf x, yâŸ«[K] := by simp[ScalarSMul.smul_eq_mul_make]; sorry_proof
  Â· sorry_proof

@[fun_prop]
theorem Inner.inner.arg_a1.IsContinuousLinearMap_rule
  {K : Type*} [RCLike K]
  {X : Type*} [TopologicalSpace X] [AddCommMonoid X] [Module K X]
  {Y : Type*} [NormedAddCommGroup Y] [AdjointSpace K Y]
  (f : X â†’ Y) (hf : IsContinuousLinearMap K f) (y : Y)
  : IsContinuousLinearMap K fun x => âŸªy, f xâŸ«[K] :=
by
  constructor
  Â· constructor
    Â· intros
      rw[hf.linear.map_add]
      rw[AdjointSpace.inner_add_right]
    Â· intros
      rw[hf.linear.map_smul]
      simp only [AdjointSpace.inner_smul_right]
      rfl
  Â· sorry_proof


@[fun_prop]
theorem Inner.inner.arg_a1.IsContinuousLinearMap_rule_real
  (f : X â†’ Y) (hf : IsContinuousLinearMap R f) (y : Y)
  : IsContinuousLinearMap R fun x => âŸªy, f xâŸ«[K] :=
by
  constructor
  Â· constructor
    Â· intros
      rw[hf.linear.map_add]
      rw[AdjointSpace.inner_add_right]
    Â· intro c x
      calc _ = âŸªy, (c â€¢ (1:K)) â€¢ f xâŸ«[K] := by simp[hf.linear.map_smul]
           _ = ((c â€¢ (1:K))) * âŸªy, f xâŸ«[K] := by rw[AdjointSpace.inner_smul_right]
           _ = c â€¢ âŸªy, f xâŸ«[K] := by simp[ScalarSMul.smul_eq_mul_make]
  Â· sorry_proof


end IsContinuousLinearMap
