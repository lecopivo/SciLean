import SciLean.Mathlib.Analysis.AdjointSpace.Basic

import SciLean.Core.FunctionPropositions.IsContinuousLinearMap
import SciLean.Core.Objects.SemiInnerProductSpace

import SciLean.Tactic.FunTrans.Elab
import SciLean.Tactic.FunTrans.Attr

set_option linter.unusedVariables false

open RCLike

open scoped ComplexConjugate

variable {ùïú E F G : Type*} [RCLike ùïú]
variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
variable [AdjointSpace ùïú E] [AdjointSpace ùïú F] [AdjointSpace ùïú G]

set_default_scalar ùïú

/-! ### Adjoint operator -/

open AdjointSpace SciLean

variable [CompleteSpace E] [CompleteSpace G]


variable (ùïú)
open Classical in
/-- The adjoint of a bounded operator from Hilbert space `E` to Hilbert space `F`. -/
@[fun_trans]
noncomputable
def adjoint (f : E ‚Üí F) : F ‚Üí E :=
  if h : ‚àÉ g : F ‚Üí E, ‚àÄ x y, ‚ü™f x, y‚ü´ = ‚ü™x, g y‚ü´ then
    choose h
  else
    0
variable {ùïú}


postfix:1000 "‚Ä†" => adjoint defaultScalar%


theorem adjoint_ex (A : E ‚Üí F) (hA : IsContinuousLinearMap ùïú A) :
    ‚àÄ x y, ‚ü™A x, y‚ü´ = ‚ü™x, (A‚Ä†) y‚ü´ := sorry_proof

theorem adjoint_clm {A : E ‚Üí F} (hA : IsContinuousLinearMap ùïú A) : IsContinuousLinearMap ùïú (A‚Ä†) :=
    sorry_proof


/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_left (A : E ‚Üí F) (hA : IsContinuousLinearMap ùïú A) (x : E) (y : F) :
    ‚ü™(A‚Ä†) y, x‚ü´ = ‚ü™y, A x‚ü´ := by
  rw[‚Üê AdjointSpace.conj_symm]
  rw[‚Üê adjoint_ex _ hA]
  rw[AdjointSpace.conj_symm]


/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_right (A : E ‚Üí F) (hA : IsContinuousLinearMap ùïú A) (x : E) (y : F) :
    ‚ü™x, (A‚Ä†) y‚ü´ = ‚ü™A x, y‚ü´ := by
  rw[‚Üê adjoint_ex _ hA]

/-- The adjoint is involutive. -/
@[simp]
theorem adjoint_adjoint (A : E ‚Üí F) (hA : IsContinuousLinearMap ùïú A) : A‚Ä†‚Ä† = A := by
  funext u
  apply AdjointSpace.ext_inner_left ùïú
  intro v
  rw[‚Üê adjoint_ex _ (adjoint_clm hA)]
  apply adjoint_inner_left _ hA


/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
theorem adjoint_comp (A : F ‚Üí G) (B : E ‚Üí F)
    (hA : IsContinuousLinearMap ùïú A) (hB : IsContinuousLinearMap ùïú B) :
    (A ‚àò B)‚Ä† = B‚Ä† ‚àò A‚Ä† := by
  funext u
  apply AdjointSpace.ext_inner_left ùïú
  intro v; dsimp
  rw[‚Üê adjoint_ex _ (by fun_prop), ‚Üê adjoint_ex _ hB,‚Üê adjoint_ex _ hA]
  rfl

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `‚ü™A x, y‚ü´ = ‚ü™x, B y‚ü´`
for all `x` and `y`. -/
theorem eq_adjoint_iff (A : E ‚Üí F) (B : F ‚Üí E) (hB : IsContinuousLinearMap ùïú B) :
    A = B‚Ä† ‚Üî ‚àÄ x y, ‚ü™A x, y‚ü´ = ‚ü™x, B y‚ü´ := by
  constructor
  . intro h x y; rw[h,adjoint_inner_left _ hB]
  . intro h; funext u
    apply AdjointSpace.ext_inner_right ùïú
    intro v
    rw[adjoint_inner_left _ hB]
    apply h u v



----------------------------------------------------------------------------------------------------

namespace adjoint


variable
  {K : Type _} [RCLike K]
  {X : Type _} [NormedAddCommGroup X] [AdjointSpace K X] [CompleteSpace X]
  {Y : Type _} [NormedAddCommGroup Y] [AdjointSpace K Y] [CompleteSpace Y]
  {Z : Type _} [NormedAddCommGroup Z] [AdjointSpace K Z] [CompleteSpace Z]
  {Œπ : Type _} [Fintype Œπ]
  {E : Œπ ‚Üí Type _} [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, AdjointSpace K (E i)] [‚àÄ i, CompleteSpace (E i)]

set_default_scalar K

@[fun_trans]
theorem adjoint_id :
    (fun x : X => x)‚Ä† = fun x => x := by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  intros; rfl


@[fun_trans]
theorem const_rule :
    (fun (x : X) =>L[K] (0 : Y))‚Ä† = fun x =>L[K] 0 := by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp

@[fun_trans]
theorem proj_rule [DecidableEq Œπ]
    (i : Œπ) :
    (fun (f : (i' : Œπ) ‚Üí E i') => f i)‚Ä†
    =
    fun x => (fun j => if h : i=j then h ‚ñ∏ x else 0) := by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  intro x y
  simp[Inner.inner]
  sorry_proof

@[fun_trans]
theorem prod_rule
    (f : X ‚Üí Y) (g : X ‚Üí Z)
    (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => (f x, g x))‚Ä†
    =
    fun yz =>
      let x‚ÇÅ := (f‚Ä†) yz.1
      let x‚ÇÇ := (g‚Ä†) yz.2
      x‚ÇÅ + x‚ÇÇ :=
by
  sorry_proof

@[fun_trans]
theorem comp_rule
    (f : Y ‚Üí Z) (g : X ‚Üí Y)
    (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f (g x))‚Ä†
    =
    fun z =>
      let y := (f‚Ä†) z
      let x := (g‚Ä†) y
      x := by
  funext u
  apply AdjointSpace.ext_inner_left K
  intro v; dsimp
  rw[‚Üê adjoint_ex _ (by fun_prop), adjoint_ex _ hf,‚Üê adjoint_ex _ hg]


@[fun_trans]
theorem let_rule
    (f : X ‚Üí Y ‚Üí Z) (g : X ‚Üí Y)
    (hf : IsContinuousLinearMap K (fun xy : X√óY => f xy.1 xy.2)) (hg : IsContinuousLinearMap K g) :
    (fun x => let y := g x; f x y)‚Ä†
    =
    fun z =>
      let xy := ((fun (x,y) => f x y)‚Ä†) z
      let x' := (g‚Ä†) xy.2
      xy.1 + x' :=
by
  have h : (fun x => let y := g x; f x y)‚Ä†
           =
           (fun x => (x, g x))‚Ä† ‚àò (fun (x,y) => f x y)‚Ä†
         := comp_rule (K:=K) (f:=_) (g:=(fun x => (x, g x))) (hf:=hf) (hg:=by fun_prop)
  rw[h]
  fun_trans
  rfl


@[fun_trans]
theorem pi_rule
    (f : X ‚Üí (i : Œπ) ‚Üí E i) (hf : ‚àÄ i, IsContinuousLinearMap K (f ¬∑ i)) :
    (fun (x : X) (i : Œπ) => f x i)‚Ä†
    =
    (fun x' => Finset.sum Finset.univ fun i => ((f ¬∑ i)‚Ä†) (x' i)) := by

  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  intro x y
  rw[AdjointSpace.sum_inner]
  simp (disch:=fun_prop) [adjoint_inner_left]
  rfl




--------------------------------------------------------------------------------
-- Function Rules --------------------------------------------------------------
--------------------------------------------------------------------------------

variable
  {K : Type _} [RCLike K]
  {X : Type _} [NormedAddCommGroup X] [AdjointSpace K X] [CompleteSpace X]
  {Y : Type _} [NormedAddCommGroup Y] [AdjointSpace K Y] [CompleteSpace Y]
  {Z : Type _} [NormedAddCommGroup Z] [AdjointSpace K Z] [CompleteSpace Z]
  {Œπ : Type _} [Fintype Œπ]
  {E : Œπ ‚Üí Type _} [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, AdjointSpace K (E i)] [‚àÄ i, CompleteSpace (E i)]

open SciLean

set_default_scalar K


-- Prod ------------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Prod.fst.arg_self.adjoint_rule
  (f : X ‚Üí Y√óZ) (hf : SciLean.IsContinuousLinearMap K f)
  : (fun x => (f x).1)‚Ä†
    =
    fun y => (f‚Ä†) (y,0) :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left]
  sorry_proof -- todo some lemma about inner product on product spaces

@[fun_trans]
theorem Prod.snd.arg_self.adjoint_rule
  (f : X ‚Üí Y√óZ) (hf : SciLean.IsContinuousLinearMap K f)
  : (fun x => (f x).2)‚Ä†
    =
    fun z => (f‚Ä†) (0,z) :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left]
  sorry_proof -- todo some lemma about inner product on product spaces


-- HAdd.hAdd -------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem HAdd.hAdd.arg_a0a1.adjoint_rule
    (f g : X ‚Üí Y) (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f x + g x)‚Ä†
    =
    fun y =>
      let x‚ÇÅ := (f‚Ä†) y
      let x‚ÇÇ := (g‚Ä†) y
      x‚ÇÅ + x‚ÇÇ := by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_add_left,AdjointSpace.inner_add_right]



-- HSub.hSub -------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem HSub.hSub.arg_a0a1.adjoint_rule
    (f g : X ‚Üí Y) (hf : IsContinuousLinearMap K f) (hg : IsContinuousLinearMap K g) :
    (fun x => f x - g x)‚Ä†
    =
    fun y =>
      let x‚ÇÅ := (f‚Ä†) y
      let x‚ÇÇ := (g‚Ä†) y
      x‚ÇÅ - x‚ÇÇ := by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_sub_left,AdjointSpace.inner_sub_right]


-- Neg.neg ---------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Neg.neg.arg_a0.adjoint_rule
  (f : X ‚Üí Y) (hf : IsContinuousLinearMap K f)
  : (fun x => - f x)‚Ä†
    =
    fun y => - (f‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.inner_neg_left,AdjointSpace.inner_neg_right]


-- HMul.hmul -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HMul.hMul.arg_a0.adjoint_rule
  (c : K) (f : X ‚Üí K) (hf : IsContinuousLinearMap K f)
  : (fun x => f x * c)‚Ä†
    =
    fun y => conj c ‚Ä¢ (f‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl

open ComplexConjugate in
@[fun_trans]
theorem HMul.hMul.arg_a1.adjoint_rule
  (c : K) (f : X ‚Üí K) (hf : IsContinuousLinearMap K f)
  : (fun x => c * f x)‚Ä†
    =
    fun y => conj c ‚Ä¢ (f‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl


-- SMul.smul -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HSMul.hSMul.arg_a0.adjoint_rule
  (x' : X) (f : X ‚Üí K) (hf : IsContinuousLinearMap K f)
  : (fun x => f x ‚Ä¢ x')‚Ä†
    =
    fun y => ‚ü™x', y‚ü´ ‚Ä¢ ((fun x =>L[K] f x)‚Ä†) 1 :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  intros; ac_rfl

open ComplexConjugate in
@[fun_trans]
theorem HSMul.hSMul.arg_a1.adjoint_rule
  (c : K) (g : X ‚Üí Y) (hg : IsContinuousLinearMap K g)
  : (fun x => c ‚Ä¢ g x)‚Ä†
    =
    fun y => (conj c) ‚Ä¢ ((fun x =>L[K] g x)‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]


-- HDiv.hDiv -------------------------------------------------------------------
--------------------------------------------------------------------------------

open ComplexConjugate in
@[fun_trans]
theorem HDiv.hDiv.arg_a0.adjoint_rule
  (f : X ‚Üí K) (c : K)
  (hf : IsContinuousLinearMap K f)
  : (fun x => f x / c)‚Ä†
    =
    fun y => (conj c)‚Åª¬π ‚Ä¢ (fun x =>L[K] f x)‚Ä† y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.inner_smul_right]
  simp [div_eq_mul_inv]
  intros; ac_rfl



-- Finset.sum ------------------------------------------------------------------
--------------------------------------------------------------------------------

open BigOperators in
@[fun_trans]
theorem Finset.sum.arg_f.adjoint_rule
  (f : X ‚Üí Œπ ‚Üí Y) (hf : ‚àÄ i, IsContinuousLinearMap K (f ¬∑ i)) (A : Finset Œπ)
  : (fun x => Finset.sum Finset.univ fun i => f x i)‚Ä†
    =
    (fun y => Finset.sum Finset.univ fun i => ((f ¬∑ i)‚Ä†) y) :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by fun_prop)).2]
  simp (disch:=fun_prop) [adjoint_inner_left,AdjointSpace.sum_inner,AdjointSpace.inner_sum]


-- d/ite -----------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem ite.arg_te.adjoint_rule
  (c : Prop) [dec : Decidable c]
  (t e : X ‚Üí Y) (ht : IsContinuousLinearMap K t) (he : IsContinuousLinearMap K e)
  : (fun x => if c then t x else e x)‚Ä†
    =
    fun y =>
      if c then (t‚Ä†) y else (e‚Ä†) y :=
by
  induction dec
  case isTrue h  => ext y; simp[h]
  case isFalse h => ext y; simp[h]

@[fun_trans]
theorem dite.arg_te.adjoint_rule
  (c : Prop) [dec : Decidable c]
  (t : c  ‚Üí X ‚Üí Y) (ht : ‚àÄ p, IsContinuousLinearMap K (t p))
  (e : ¬¨c ‚Üí X ‚Üí Y) (he : ‚àÄ p, IsContinuousLinearMap K (e p))
  : (fun x => if h : c then t h x else e h x)‚Ä†
    =
    fun y =>
      if h : c then ((t h ¬∑)‚Ä†) y else ((e h ¬∑)‚Ä†) y :=
by
  induction dec
  case isTrue h  => ext y; simp[h]
  case isFalse h => ext y; simp[h]



-- Inner -----------------------------------------------------------------------
--------------------------------------------------------------------------------

@[fun_trans]
theorem Inner.inner.arg_a1.adjoint_rule
  (f : X ‚Üí Y) (hf : IsContinuousLinearMap K f) (y : Y)
  : (fun x => ‚ü™y, f x‚ü´)‚Ä†
    =
    fun z => z ‚Ä¢ (f‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by sorry_proof)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.conj_symm]


section OnRealSpace

variable
  {R : Type _} [RealScalar R]
  {X : Type _} [NormedAddCommGroup X] [AdjointSpace R X] [CompleteSpace X]
  {Y : Type _} [NormedAddCommGroup Y] [AdjointSpace R Y] [CompleteSpace Y]

open SciLean

set_default_scalar R

-- inner product is not ‚ÑÇ-linear in its first argument thus it can't have an adjoint
open ComplexConjugate in
@[fun_trans]
theorem Inner.inner.arg_a0.adjoint_rule
  (f : X ‚Üí Y) (hf : IsContinuousLinearMap R f) (y : Y)
  : (fun x => ‚ü™f x, y‚ü´)‚Ä†
    =
    fun z => (conj z) ‚Ä¢ (f‚Ä†) y :=
by
  rw[‚Üê (eq_adjoint_iff _ _ (by sorry_proof)).2]
  simp (disch:=fun_prop)
    [adjoint_inner_left,AdjointSpace.inner_smul_left,AdjointSpace.conj_symm]
  intros
  rw[‚Üê AdjointSpace.conj_symm]; simp


end OnRealSpace
