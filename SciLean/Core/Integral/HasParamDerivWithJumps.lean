import Mathlib.Analysis.Calculus.FDeriv.Basic
import Mathlib.Analysis.RCLike.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpace
import Mathlib.Data.Erased
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.MeasureTheory.Decomposition.Lebesgue
import Mathlib.MeasureTheory.Measure.Hausdorff

import SciLean.Core.NotationOverField
import SciLean.Mathlib.Analysis.AdjointSpace.Adjoint

import SciLean.Core.FunctionTransformations.RevFDeriv

set_option linter.unusedVariables false

open MeasureTheory Topology Filter

namespace SciLean

variable
  {R} [RealScalar R] [MeasureSpace R]
  {W} [NormedAddCommGroup W] [NormedSpace R W]
  {X} [NormedAddCommGroup X] [AdjointSpace R X] [CompleteSpace X] [MeasureSpace X] [BorelSpace X]
  {Y} [NormedAddCommGroup Y] [NormedSpace R Y] [NormedSpace â„ Y]
  {Z} [NormedAddCommGroup Z] [NormedSpace R Z] [NormedSpace â„ Z]

set_default_scalar R


variable (R)
open Classical in
noncomputable
def frontierSpeed (A : W â†’ Set X) (w dw : W) (x : X) : R :=
  match Classical.dec (âˆƒ (Ï† : W â†’ X â†’ R), (âˆ€ w, A w = {x | Ï† w x = 0})) with
  | .isTrue h =>
    let Ï† := Classical.choose h
    (-(fderiv R (Ï† Â· x) w dw)/â€–fgradient (Ï† w Â·) xâ€–â‚‚)
  | .isFalse _ => 0


structure HasParamFDerivWithJumpsAtImpl (f : W â†’ X â†’ Y) (w : W)
    (f' : W â†’ X â†’ Y)
    /- Index set for jump discontinuities -/
    (I : Type)
    /- Index set for domains. -/
    (J : Type)
    /- Given to domain indices `i` and `j` return the index `k` of the interface `Î“â‚– = Î©áµ¢ âˆ© Î©â±¼`. -/
    (Î¹ : J â†’ J â†’ Option I)
    /- Domains on which `f` is differentiable w.r.t. `w`.  -/
    (Î© : J â†’ W â†’ Set X)
    /- Values of `f` on both sides of jump discontinuity.

    The first value is in the positive noramal direction and the second value in the negative
    normal direction.

    The orientation of the normal is arbitrary but fixed as `jumpVals` and `jumpSpeed` depend on it. -/
    (jumpVals : I â†’ X â†’ YÃ—Y)
    /- Normal speed of the jump discontinuity. -/
    (jumpSpeed : I â†’ W â†’ X â†’ R)
    /- Jump discontinuities of `f`. -/
    (jump : I â†’ Set X) : Prop where

  -- todo: some of there statements should hold on neighbourhoods of `w`
  diff :  âˆ€ j x, x âˆˆ Î© j w â†’ DifferentiableAt R (f Â· x) w
  deriv : âˆ€ j x dw, x âˆˆ Î© j w â†’ fderiv R (f Â· x) w dw = f' dw x

  jumpValsLimit :
    âˆ€ p n : J, match Î¹ p n with
      | none => True
      | some i => âˆ€ x âˆˆ jump i,
        /- lim x' â†’ x, x âˆˆ Î© p, f w x' = (jumpVals i x).1 -/
        (ð“ x âŠ“ ð“Ÿ (Î© p w)).Tendsto (fun x' => f w x') (ð“ (jumpVals i x).1)
        âˆ§
        /- lim x' â†’ x, x âˆˆ Î© n, f w x' = (jumpVals i x).2 -/
        (ð“ x âŠ“ ð“Ÿ (Î© n w)).Tendsto (fun x' => f w x') (ð“ (jumpVals i x).2)

  jumpSpeedEq :
    âˆ€ p n : J, match Î¹ p n with
      | none => True
      | some i => âˆ€ x âˆˆ jump i,
        frontierSpeed R (Î© n) w dw x = jumpSpeed i dw x


def HasParamFDerivWithJumpsAt (f : W â†’ X â†’ Y) (w : W)
    (f' : W â†’ X â†’ Y)
    (I : Type)
    /- Values of `f` on both sides of jump discontinuity.

    The first value is in the positive noramal direction and the second value in the negative
    normal direction.

    The orientation of the normal is arbitrary but fixed as `jumpVals` and `jumpSpeed` depend on it. -/
    (jumpVals : I â†’ X â†’ YÃ—Y)
    /- Normal speed of the jump discontinuity. -/
    (jumpSpeed : I â†’ W â†’ X â†’ R)
    /- Jump discontinuities of `f`. -/
    (jump : I â†’ Set X) : Prop := âˆƒ J Î© Î¹, HasParamFDerivWithJumpsAtImpl R f w f' I J Î¹ Î© jumpVals jumpSpeed jump


def HasParamFDerivWithJumps (f : W â†’ X â†’ Y)
    (f' : W â†’ W â†’ X â†’ Y)
    (I : Type)
    (jumpVals : I â†’ W â†’ X â†’ YÃ—Y)
    (jumpSpeed : I â†’ W â†’ W â†’ X â†’ R)
    (jump : I â†’ W â†’ Set X) := âˆ€ w, HasParamFDerivWithJumpsAt R f w (f' w) I (jumpVals Â· w) (jumpSpeed Â· w) (jump Â· w)


open FiniteDimensional
-- @[fun_trans]
theorem fderiv_under_integral
  {X} [NormedAddCommGroup X] [AdjointSpace R X] [CompleteSpace X] [MeasureSpace X] [BorelSpace X]
  (f : W â†’ X â†’ Y) (w : W) (Î¼ : Measure X)
  {I} [IndexType I] {f' df s S}
  (hf : HasParamFDerivWithJumpsAt R f w f' I df s S) (dw : W)
  /- todo: add some integrability conditions -/ :
  (fderiv R (fun w' => âˆ« x, f w' x âˆ‚Î¼) w dw)
  =
  let interior := âˆ« x, f' dw x âˆ‚Î¼
  let density := fun x => Scalar.ofENNReal (R:=R) (Î¼.rnDeriv volume x)
  let shocks := âˆ‘ i, âˆ« x in S i, (s i dw x * density x) â€¢ ((df i x).1 - (df i x).2) âˆ‚Î¼H[finrank R X - 1]
  interior + shocks := sorry_proof


----------------------------------------------------------------------------------------------------
-- Lambda rules ------------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

namespace HasParamFDerivWithJumpsAt

@[aesop unsafe]
theorem smooth_rule
    (w : W)
    (f : W â†’ X â†’ Y) (hf : âˆ€ x, DifferentiableAt R (f Â· x) w) :
    HasParamFDerivWithJumpsAt R f w (fun dw x => fderiv R (f Â· x) w dw) Empty 0 0 (fun _ => âˆ…) :=

  sorry_proof

@[aesop unsafe]
theorem comp_smooth_jumps_rule
    (f : W â†’ Y â†’ Z) (g : W â†’ X â†’ Y) (w : W)
    {I g' bg sg Sg}
    (hf : Differentiable R (fun (w,y) => f w y))
    (hg : HasParamFDerivWithJumpsAt R g w g' I bg sg Sg) :
    HasParamFDerivWithJumpsAt R (fun w x => f w (g w x)) w
      /- f' -/
      (fun dw x =>
         let y := g w x
         let dy := g' dw x
         let dz := fderiv R (fun (w,y) => f w y) (w,y) (dw,dy)
         dz)
      (I)
      /- jumpVals -/
      (fun i x =>
         let y := bg i x
         (f w y.1, f w y.2))
      /- jumpSpeed -/
      (sg)
      /- jump -/
      (Sg) := sorry_proof

@[aesop unsafe]
theorem comp_smooth_jumps_rule_at
    (f : W â†’ Y â†’ Z) (g : W â†’ X â†’ Y) (w : W)
    {I g' bg sg Sg}
    (hf : âˆ€ x, DifferentiableAt R (fun (w,y) => f w y) (w,g w x))
    (hg : HasParamFDerivWithJumpsAt R g w g' I bg sg Sg) :
    HasParamFDerivWithJumpsAt R (fun w x => f w (g w x)) w
      /- f' -/
      (fun dw x =>
         let y := g w x
         let dy := g' dw x
         let dz := fderiv R (fun (w,y) => f w y) (w,y) (dw,dy)
         dz)
      (I)
      /- jumpVals -/
      (fun i x =>
         let y := bg i x
         (f w y.1, f w y.2))
      /- jumpSpeed -/
      (sg)
      /- jump -/
      (Sg) := sorry_proof



end HasParamFDerivWithJumpsAt


----------------------------------------------------------------------------------------------------
-- Function Rules ----------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

open FiniteDimensional in
/--
Proposition stating that intersection of two jump discontinuities is empty up to
(n-1)-dimensional measure. -/
def DisjointJumps {X} [NormedAddCommGroup X] [NormedSpace R X] [MeasureSpace X] [BorelSpace X]
  {I J} (S : I â†’ Set X) (P : J â†’ Set X) :=
  Î¼H[finrank R X - 1] (â‹ƒ i, S i âˆ© â‹ƒ j, P j) = 0


@[aesop safe]
theorem Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule
    (f : W â†’ X â†’ Y) (g : W â†’ X â†’ Z) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg)
    /- (hIJ : DisjointJumps R Sf Sg) -/ :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => (f w x, g w x)) w
      (f' := fun dw x => (f' dw x, g' dw x))
      (I := IâŠ•J)
      (jumpVals := Sum.elim
           (fun i x =>
             let (yâ‚, yâ‚‚) := bf i x
             let z := g w x
             ((yâ‚,z), (yâ‚‚,z)))
           (fun j x =>
             let y := f w x
             let (zâ‚, zâ‚‚) := bg j x
             ((y,zâ‚), (y,zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := sorry_proof


@[aesop safe]
theorem Prod.fst.arg_self.HasParamFDerivWithJumpsAt_rule
    (f : W â†’ X â†’ YÃ—Z) (w : W)
    {I f' bf sf Sf}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => (f w x).1) w
      (f':= fun dw x => (f' dw x).1)
      (I := I)
      (jumpVals := fun i x => let y := bf i x; (y.1.1, y.2.1))
      (jumpSpeed := sf)
      (jump := Sf) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (fun _ x => Prod.fst x) f w
          (by fun_prop) hf
  fun_trans


@[aesop safe]
theorem Prod.snd.arg_self.HasParamFDerivWithJumpsAt_rule
    (f : W â†’ X â†’ YÃ—Z) (w : W)
    {I f' bf sf Sf}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => (f w x).2) w
      (f':= fun dw x => (f' dw x).2)
      (I := I)
      (jumpVals := fun i x => let y := bf i x; (y.1.2, y.2.2))
      (jumpSpeed := sf)
      (jump := Sf) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (fun _ x => Prod.snd x) f w
          (by fun_prop) hf
  fun_trans


@[aesop safe]
theorem HAdd.hAdd.arg_a0a1.HasParamFDerivWithJumpsAt_rule
    (f g : W â†’ X â†’ Y) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => f w x + g w x) w
      (f' := fun (dw : W) x =>
        f' dw x + g' dw x)
      (I:=IâŠ•J)
      (jumpVals := Sum.elim
        (fun i x =>
          let (yâ‚, yâ‚‚) := bf i x
          let z := g w x
          ((yâ‚+z), (yâ‚‚+z)))
        (fun j x =>
          let y := f w x
          let (zâ‚, zâ‚‚) := bg j x
          ((y+zâ‚), (y+zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (f:=fun _ (y:YÃ—Y) => y.1 + y.2) (g:=fun w x => (f w x, g w x)) (w:=w)
          (hf:=by fun_prop)
          (hg:= Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule R f g w hf hg)
  . fun_trans
  . rename_i i x
    induction i
    . simp
    . simp


@[aesop safe]
theorem HSub.hSub.arg_a0a1.HasParamFDerivWithJumpsAt_rule
    (f g : W â†’ X â†’ Y) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => f w x - g w x) w
      (f' := fun (dw : W) x =>
        f' dw x - g' dw x)
      (I:=IâŠ•J)
      (jumpVals := Sum.elim
        (fun i x =>
          let (yâ‚, yâ‚‚) := bf i x
          let z := g w x
          ((yâ‚-z), (yâ‚‚-z)))
        (fun j x =>
          let y := f w x
          let (zâ‚, zâ‚‚) := bg j x
          ((y-zâ‚), (y-zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (f:=fun _ (y:YÃ—Y) => y.1 - y.2) (g:=fun w x => (f w x, g w x)) (w:=w)
          (hf:=by fun_prop)
          (hg:= Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule R f g w hf hg)
  . fun_trans
  . rename_i i x
    induction i
    . simp
    . simp

@[aesop safe]
theorem Neg.neg.arg_a0.HasParamFDerivWithJumpsAt_rule
    (f : W â†’ X â†’ Y) (w : W)
    {I f' bf sf Sf}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => - f w x) w
      (f':=fun dw x => - f' dw x)
      (I := I)
      (jumpVals := fun i x => - bf i x)
      (jumpSpeed := sf)
      (jump := Sf) := by


  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (f:=fun _ y => - y) (g:=f) (w:=w)
          (hf:=by fun_prop)
          (hg:=hf)
  . fun_trans


@[aesop safe]
theorem HMul.hMul.arg_a0a1.HasParamFDerivWithJumpsAt_rule
    (f g : W â†’ X â†’ R) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => f w x * g w x) w
      (f' := fun (dw : W) x =>
         let y := f w x
         let dy := f' dw x
         let z := g w x
         let dz := g' dw x
         dy * z + y * dz)
      (I:=IâŠ•J)
      (jumpVals := Sum.elim
        (fun i x =>
          let (yâ‚, yâ‚‚) := bf i x
          let z := g w x
          ((yâ‚*z), (yâ‚‚*z)))
        (fun j x =>
          let y := f w x
          let (zâ‚, zâ‚‚) := bg j x
          ((y*zâ‚), (y*zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (f:=fun _ (y:RÃ—R) => y.1 * y.2) (g:=fun w x => (f w x, g w x)) (w:=w)
          (hf:=by simp; fun_prop)
          (hg:= Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule R f g w hf hg)
  . fun_trans; ac_rfl
  . rename_i i x
    induction i
    . simp
    . simp


@[aesop safe]
theorem HSMul.hSMul.arg_a0a1.HasParamFDerivWithJumpsAt_rule
    (f : W â†’ X â†’ R) (g : W â†’ X â†’ Y) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => f w x â€¢ g w x) w
      (f' := fun (dw : W) x =>
         let y := f w x
         let dy := f' dw x
         let z := g w x
         let dz := g' dw x
         dy â€¢ z + y â€¢ dz)
      (I:=IâŠ•J)
      (jumpVals := Sum.elim
        (fun i x =>
          let (yâ‚, yâ‚‚) := bf i x
          let z := g w x
          ((yâ‚â€¢z), (yâ‚‚â€¢z)))
        (fun j x =>
          let y := f w x
          let (zâ‚, zâ‚‚) := bg j x
          ((yâ€¢zâ‚), (yâ€¢zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule (R:=R)
          (f:=fun _ (y:RÃ—Y) => y.1 â€¢ y.2) (g:=fun w x => (f w x, g w x)) (w:=w)
          (hf:=by simp; fun_prop)
          (hg:= Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule R f g w hf hg)
  . fun_trans; ac_rfl
  . rename_i i x
    induction i
    . simp
    . simp


@[aesop safe]
theorem HDiv.hDiv.arg_a0a1.HasParamFDerivWithJumpsAt_rule
    (f g : W â†’ X â†’ R) (w : W)
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg)
    (hg' : âˆ€ x, g w x â‰  0) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => f w x / g w x) w
      (f' := fun (dw : W) x =>
         let y := f w x
         let dy := f' dw x
         let z := g w x
         let dz := g' dw x
         (dy * z - y * dz) / (z^2))
      (I:=IâŠ•J)
      (jumpVals := Sum.elim
        (fun i x =>
          let (yâ‚, yâ‚‚) := bf i x
          let z := g w x
          ((yâ‚/z), (yâ‚‚/z)))
        (fun j x =>
          let y := f w x
          let (zâ‚, zâ‚‚) := bg j x
          ((y/zâ‚), (y/zâ‚‚))))
      (jumpSpeed := Sum.elim sf sg)
      (jump := Sum.elim Sf Sg) := by

  convert HasParamFDerivWithJumpsAt.comp_smooth_jumps_rule_at (R:=R)
          (f:=fun _ (y:RÃ—R) => y.1 / y.2) (g:=fun w x => (f w x, g w x)) (w:=w)
          (hf:=by simp; sorry_proof)
          (hg:= Prod.mk.arg_fstsnd.HasParamFDerivWithJumpsAt_rule R f g w hf hg)
  . fun_trans (disch:=apply hg')
  . rename_i i x
    induction i
    . simp
    . simp


@[aesop safe]
theorem ite.arg_te.HasParamFDerivWithJumpsAt_rule
    (f g : W â†’ X â†’ Y) (w : W)
    {c : W â†’ X â†’ Prop} [âˆ€ w x, Decidable (c w x)]
    {f' I bf sf Sf} {g' J bg sg Sg}
    (hf : HasParamFDerivWithJumpsAt R f w f' I bf sf Sf)
    (hg : HasParamFDerivWithJumpsAt R g w g' J bg sg Sg) :
    HasParamFDerivWithJumpsAt (R:=R) (fun w x => if c w x then f w x else g w x) w
      (f' := fun dw x => if c w x then f' dw x else g' dw x)
      (I:=UnitâŠ•IâŠ•J)
      (jumpVals :=
        Sum.elim
         (fun _ x => (f w x, g w x)) <|
        Sum.elim bf bg)
      (jumpSpeed := Sum.elim (fun _ => frontierSpeed R (fun w => {x | Â¬c w x}) w) (Sum.elim sf sg))
      (jump := Sum.elim (fun _ => frontier {x | c w x}) <|
               Sum.elim
                 (fun i => Sf i âˆ© {x | c w x})
                 (fun j => Sg j âˆ© {x | Â¬c w x})) := by

  sorry_proof
