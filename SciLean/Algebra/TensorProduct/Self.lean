import SciLean.Algebra.TensorProduct.Basic
import SciLean.Analysis.AdjointSpace.CanonicalBasis

namespace SciLean

/--
Class providing identity matrix of type `X ‚äó X`
 -/
class TensorProductSelf
    (R X XX : Type*) [RCLike R]
    [NormedAddCommGroup X] [AdjointSpace R X]
    [AddCommGroup XX] [Module R XX]
    [TensorProductType R X X XX]
  where
    /-- Identit matrix `ùêà` -/
    identityMatrix : XX
    identityMatrix_spec (x : X) :
      matVecMulAdd (1:R) identityMatrix x 0 0 = x

    /-- `addIdentityMatrix a A = A + a‚Ä¢ùêà` - adds `a` multiple of identity to `A` -/
    addIdentityMatrix (a : R) (A : XX) : XX
    addIdentityMatrix_spec (a : R) (A : XX) :
      addIdentityMatrix a A = a ‚Ä¢ identityMatrix + A

export TensorProductSelf (identityMatrix addIdentityMatrix)

section Self

variable
    {R X XX : Type*} [RCLike R]
    [NormedAddCommGroup X] [AdjointSpace R X]
    [AddCommGroup XX] [Module R XX]
    [TensorProductType R X X XX]
    [ts : TensorProductSelf R X XX]


theorem addIdentityMatrix_def (a : R) (A : XX) :
  ts.addIdentityMatrix a A = A + a‚Ä¢ts.identityMatrix := sorry_proof


section MatVecNotation

variable [TensorProductGetY R X X XX]

@[simp, simp_core]
theorem vecMatMul_identityMatrix (x : X) : ts.identityMatrix * x = x := sorry_proof

@[simp, simp_core]
theorem smul_vecMatMul_identityMatrix (a : R) (x : X) : (a ‚Ä¢ ts.identityMatrix) * x = a‚Ä¢x := by
  simp[matVecMul_smul_assoc]

end MatVecNotation

section VecMatNotation

variable [TensorProductGetX R X X XX]

@[simp, simp_core]
theorem matVecMul_identityMatrix (x : X) : x * ts.identityMatrix = x := sorry_proof

end VecMatNotation


@[simp, simp_core]
theorem matVecMulAdd_identityMatrix (a b : R) (x y : X) :
    matVecMulAdd a ts.identityMatrix x b y
    =
    a‚Ä¢x+b‚Ä¢y := by
  have : TensorProductGetY R X X XX := ‚ü®‚ü©
  simp[matVecMulAdd_def]

@[simp, simp_core]
theorem matVecMulAdd_addIdentityMatrix (a b c : R) (A : XX) (x y : X) :
    matVecMulAdd a (ts.addIdentityMatrix c A) x b y
    =
    matVecMulAdd a A x 1 ((a*c)‚Ä¢x + b‚Ä¢y) := by
  have : TensorProductGetY R X X XX := ‚ü®‚ü©
  simp[matVecMulAdd_def,addIdentityMatrix_def,add_matVecMul,matVecMul_smul_assoc]
  module

@[simp, simp_core]
theorem matHVecMulAdd_identityMatrix (a b : R) (x y : X) :
    matHVecMulAdd a ts.identityMatrix x b y
    =
    a‚Ä¢x+b‚Ä¢y := by
  have : TensorProductGetX R X X XX := ‚ü®‚ü©
  simp[matHVecMulAdd_def]

@[simp, simp_core]
theorem matHVecMulAdd_addIdentityMatrix (a b c : R) (A : XX) (x y : X) :
    matHVecMulAdd a (ts.addIdentityMatrix c A) x b y
    =
    matHVecMulAdd a A x 1 ((a*c)‚Ä¢x + b‚Ä¢y) := by
  have : TensorProductGetX R X X XX := ‚ü®‚ü©
  simp[matHVecMulAdd_def,addIdentityMatrix_def,vecMatMul_add,vecMatMul_smul_assoc]
  module

@[simp, simp_core]
theorem addIdentityMatrix_zero (a : R) :
    ts.addIdentityMatrix a (0 : XX) = a‚Ä¢ts.identityMatrix := by
  simp[addIdentityMatrix_def]

end Self

----------------------------------------------------------------------------------------------------
-- Notation for Identity Matrix --------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

open Lean Meta Elab Term Qq in
/--
`ùêà[ùïú,X]` is identity matrix on `X` which is a vector space over field ùïú

`ùêà[ùïú,n]` is identityt matrix on `ùïú^[n]` where `ùïú` is the default scalar
-/
elab (priority:=high) "ùêà[" k:term "," X:term "]" : term <= XX => do

  -- if `X` is natural number
  try
    let n ‚Üê elabTermAndSynthesize X q(Nat)
    let K ‚Üê elabTerm k none
    let I ‚Üê mkAppM ``Idx #[n]
    let X ‚Üê mkAppOptM ``DataArrayN #[K, none, I,none,none]
    let XX ‚Üê mkAppOptM ``DataArrayN #[K, none, (‚Üê mkAppM ``Prod #[I,I]), none,none]
    let id ‚Üê mkAppOptM ``identityMatrix #[K,X,XX,none,none,none,none,none,none,none]
    return id
  catch _ =>
    pure ()

  -- if we know the resulting type
  if ¬¨XX.isMVar then
    let K ‚Üê elabTerm k none
    let X ‚Üê elabTerm X none
    if K.isMVar ‚à® X.isMVar then
      let cls := mkAppN (‚Üê mkConstWithFreshMVarLevels ``TensorProductGetRXY) #[K,X,X,XX]
      let _ ‚Üê synthInstance cls
    let id ‚Üê mkAppOptM ``identityMatrix #[K,X,XX,none,none,none,none,none,none,none]
    return id
  else
    let cls ‚Üê elabTerm (‚Üê `(TensorProductGetYX $k $X $X _)) none
    let _ ‚Üê synthInstance cls
    let K := cls.getArg! 0
    let X := cls.getArg! 1
    let XX := cls.appArg!
    let id ‚Üê mkAppOptM ``identityMatrix #[K,X,XX,none,none,none,none,none,none,none]
    return id


/--
`ùêà[X]` is the identity matrix for space `X`.

`ùêà[n]` is identityt matrix on `R^[n]` where `R` is the default scalar
 -/
macro "ùêà[" X:term "]" : term => `(ùêà[defaultScalar%, $X])

/-- `ùêà` is the identity Matrix  -/
macro "ùêà" : term => `(ùêà[defaultScalar%, _])

@[app_unexpander identityMatrix] def unexpandIdentityMatrix : Lean.PrettyPrinter.Unexpander
  | `($(_) $_ $_) => `(ùêà)
  | _ => throw ()



----------------------------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------
----------------------------------------------------------------------------------------------------

/--
Class providing operations on diagonals of matrices of type `X ‚äó X`

Is there basis free version?
 -/
class TensorProductDiag
    (R X XX : Type*) [RCLike R]
    [NormedAddCommGroup X] [AdjointSpace R X]
    [AddCommGroup XX] [Module R XX]
    [tp : TensorProductType R X X XX]
    [Fintype I] [CanonicalBasis I R X]
  where

    /-- Turn vector `x` into diagonal matrix -/
    diagonal (x : X) : XX
    diagonal_spec : ‚àÄ (x : X) ,
      (diagonal x)
      =
      -- ‚àë i, (‚Ñº i x) ‚Ä¢ (‚Öá i) ‚äó (‚Öá i)
      Finset.univ.sum fun (i : I) =>
        (‚Ñº[R,i] x) ‚Ä¢ (tmulAdd (1:R) ‚Öá[R,X,i] ‚Öá'[R,X,i] 0)

    /-- Turn vector `x` into diagonal matrix -/
    diag (A : XX) : X
    diag_spec : ‚àÄ (A : XX) (i : I) ,
      ‚Ñº[R,i] (diag A)
      =
      -- ‚Ñº[i] (A * ‚Öá[i])
      ‚Ñº[R,i] (tp.matVecMulAdd (1:R) A ‚Öá[R,X,i] 0 0)

    addDiag (a : R) (x : X) (A : XX) : XX
    addDiag_spec (a : R) (x : X) (A : XX) :
      addDiag a x A
      =
      a ‚Ä¢ diagonal x + A
